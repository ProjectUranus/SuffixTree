plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'com.projecturanus'
archivesBaseName = 'suffixtree'
version '1.0'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

sourceCompatibility = targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'it.unimi.dsi:fastutil:[6.1.0,)'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = nexusUsername
                password = nexusPassword
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            pom {
                groupId = 'com.projecturanus'
                artifactId = 'suffixtree'
                name = 'suffixtree'
                description = 'Suffix Tree'
                url = 'https://www.projecturanus.com/2021/09/22/Suffix-Tree/'
                from components.java
                developers {
                    developer {
                        name = 'Lasm Gratel'
                        email = 'lasm_gratel@hotmail.com'
                    }
					developer {
						name = 'mezz'
					}
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:ProjectUranus/SuffixTree.git'
                    developerConnection = 'scm:git:git@github.com:ProjectUranus/SuffixTree.git'
                    url = 'https://github.com/ProjectUranus/SuffixTree/'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
